plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'dev.architectury.loom'
}

base {
    archivesName = "${mod_id}-forge"
}

loom {
    forge {
        mixinConfig "${mod_id}.mixins.json"
        mixinConfig "${mod_id}.forge.mixins.json"

        convertAccessWideners = true
    }

    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }

    runs {
        client {
            client()
            setConfigName("Forge Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Forge Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

repositories {
    maven {
        name = "Sinytra"
        url = "https://maven.su5ed.dev/releases"
    }
    mavenLocal()
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    forge "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    mappings "net.fabricmc:yarn:${yarn_version}:v2"

    compileOnly project(":common")

    modCompileOnly("dev.isxander.yacl:yet-another-config-lib-forge:${project.yacl_version}")

    implementation(annotationProcessor("io.github.llamalad7:mixinextras-common:${project.mixin_extras_version}"))
    implementation(include("io.github.llamalad7:mixinextras-forge:${project.mixin_extras_version}"))
}

tasks.withType(JavaCompile).configureEach {
    source(project(":common").sourceSets.main.allSource)
}
tasks.withType(Javadoc).configureEach {
    source(project(":common").sourceSets.main.allJava)
}
tasks.named("sourcesJar", Jar) {
    from(project(":common").sourceSets.main.allSource)
}

// Prevent forge from shouting at me for not having javadocs everywhere
javadoc.enabled = false

processResources {
    from project(":common").sourceSets.main.resources
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId base.archivesName.get()
            from components.java
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}

publishMods {
    file = remapJar.archiveFile
    displayName = "${property('mod_version')} for Forge ${property('minecraft_version')}"
    version = "${project.version}-forge"
    changelog = getRootProject().file("CHANGELOG.md").text
    type = STABLE
    modLoaders.add("forge")
    modLoaders.add("neoforge")

    def min = property('publish_target_min')
    def max = property('publish_target_max')

    modrinth {
        projectId = property('mod_modrinth')
        accessToken = providers.gradleProperty('enjaraiModrinthToken').get()

        if (min == max) {
            minecraftVersions.add(min)
        } else {
            minecraftVersionRange {
                start = min
                end = max
            }
        }

        requires { slug = "forgified-fabric-api" }
    }

    curseforge {
        projectId = property('mod_curseforge')
        accessToken = providers.gradleProperty('enjaraiCurseforgeToken').get()

        if (min == max) {
            minecraftVersions.add(min)
        } else {
            minecraftVersionRange {
                start = min
                end = max
            }
        }

        requires { slug = "forgified-fabric-api" }
    }

    github {
        repository = property('mod_github')
        accessToken = providers.gradleProperty('enjaraiGithubToken').get()

        commitish = "1.20/oh-boy" // TODO
        tagName = "${project.version}-forge"
    }
}
